FROM codellama:13b-instruct-q5_K_M

# Note that this is just an example, any type of GBNF compatible
# grammar can be specified. More information about the grammar
# syntax and other examples at:
#
# https://github.com/ggerganov/llama.cpp/blob/master/grammars/README.md

# JSON grammar provided by llama.cpp
# MIT License
# Copyright (c) 2023 Georgi Gerganov

PARAMETER grammar """
root   ::= object
value  ::= object | array | string | number | ("true" | "false" | "null") ws

object ::=
  "{" ws (
            string ":" ws value
    ("," ws string ":" ws value)*
  )? "}" ws

array  ::=
  "[" ws (
            value
    ("," ws value)*
  )? "]" ws

string ::=
  "\"" (
    [^"\\] |
    "\\" (["\\/bfnrt] | "u" [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]) # escapes
  )* "\"" ws

number ::= ("-"? ([0-9] | [1-9] [0-9]*)) ("." [0-9]+)? ([eE] [-+]? [0-9]+)? ws

# Optional space: by convention, applied in this grammar after literal chars when allowed
ws ::= ([ \t\n] ws)?
"""
